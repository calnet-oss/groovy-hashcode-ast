//
// This is a Gradle multi-project build.
// See settings.gradle for the subprojects.
//
// The two subprojects are:
// groovy-hashcode-ast-library - The main library that is packaged as a jar.
// groovy-hashcode-ast-test - Tests using the library.
//
// We do it this way because it's difficult to have AST transformers and
// integration tests in the same project.
//
// Gradle Multi-Project documentation is here:
// https://docs.gradle.org/current/userguide/multi_project_builds.html
//

version projectVersion
group projectGroup

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for defaultCentral_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("defaultCentral_proxy_url")) {
            maven {
                url project.property("defaultCentral_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        else {
            jcenter()
        }
    }
}

def pdep(depstr) {
    return (depstr.startsWith(":") ? project(depstr) : depstr)
}
