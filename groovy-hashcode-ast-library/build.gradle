apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'distribution'
apply plugin: 'maven-publish'

project.group = 'edu.berkeley.calnet.groovy.libs'

targetCompatibility = 1.7
sourceCompatibility = 1.7

repositories {
    //mavenLocal()
    // If using a Maven proxy, put the property settings in
    // ~/.gradle/gradle.properties for default_proxy_url,
    // default_proxy_username and default_proxy_password.
    if (project.hasProperty("default_proxy_url")) {
        maven {
            url project.property("default_proxy_url")
            credentials {
                username project.property("default_proxy_username")
                password project.property("default_proxy_password")
            }
        }
    }
    //jcenter()
    //mavenCentral()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.4"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

// publishing with './gradlew publish'
publish.dependsOn assemble
publishing {
    publications {
        maven(MavenPublication) {
            artifactId "hashcode-ast"
            artifact("${project.name}-${project.version}.zip")
        }
    }

    repositories {
        maven {
            // Put the property settings in ~/.gradle/gradle.properties for
            // default_deploy_url, default_deploy_grails_plugin_repo
            // default_deploy_username and default_deploy_password.
            url "${project.property('default_deploy_url')}/${default_deploy_grails_plugin_repo}"
            credentials {
                username project.property("default_deploy_user")
                password project.property("default_deploy_password")
            }
        }
    }
}
